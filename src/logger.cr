class Logger
  enum Level
    Debug
    Info
    Warning
    Error
    def color
      case self
      when Level::Debug
        "\033[90m > "
      when Level::Info
        "\033[94m ğŸ›ˆ "
      when Level::Warning
        "\033[33m âš ï¸ "
      when Level::Error
        "\033[31m ğŸ›‘"
      end
    end
  end

  getter file_path : String = ""
  @indent : UInt32 = 0
  @out : IO = STDOUT
  property level : Level

  def initialize(@level : Level = Level::Debug, @out : IO = STDOUT, file_path : (String | Nil) = nil)
    if file_path && !file_path.ends_with?(':')
      @file_path = file_path + ':'
    else
      @file_path = file_path || ""
    end
  end

  macro def_log(level)
    def {{level.id.downcase}}(loc : Location, msg : String)
      if @level <= Level::{{level}}
        # @out.puts "#{"  " * @indent}#{ Level::{{level}}.color } #{loc} #{msg}\033[0m"
        @out << " " * @indent
        @out << Level::{{level}}.color
        @out << file_path << loc
        @out << ' ' << msg << "\033[0m"
        @out << '\n'
      end
    end

    def {{level.id.downcase}}_descend
      @indent += 1 if @level <= Level::{{level}}
    end

    def {{level.id.downcase}}_ascend
      @indent -= 1 if @level <= Level::{{level}}
    end

    def {{level.id.downcase}}_descend(&block)
      {{level.id.downcase}}_descend
      yield
    ensure
      {{level.id.downcase}}_ascend
    end

    def {{level.id.downcase}}_descend(loc : Location, msg : String, &block)
      {{level.id.downcase}}(loc, msg)
      {{level.id.downcase}}_descend
      yield
    ensure
      {{level.id.downcase}}_ascend
    end
  end

  def_log Debug
  def_log Info
  def_log Warning
  def_log Error
end